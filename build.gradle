buildscript {
    repositories {
        maven { url 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}
plugins {
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'maven-publish'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

apply plugin: 'org.spongepowered.mixin'

version = "${modVersion}"
group = "com.github.${groupId}"
archivesBaseName = "${modId}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'parchment', version: "${parchmentVersion}-${minecraftVersion}"
    copyIdeResources = true

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.logging.mojang.level', 'debug'
            property 'mixin.debug.export', 'true'
            arg "-mixin.config=${modId}.mixins.json"

            mods {
                reincarnation_plus {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('runServer')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.debug.export', 'true'

            arg "nogui"

            mods {
                reincarnation_plus {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', "${modId}", '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/'), '--existing-mod', "manascore"

            mods {
                reincarnation_plus {
                    source sourceSets.main
                }
            }
        }
    }
}

jarJar.enable()

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    maven { url = 'https://maven.minecraftforge.net' }

    maven {
        url = 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
    }

    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev"
    }

    maven { url = "https://www.cursemaven.com" } // CurseForge

    maven { url "https://dl.cloudsmith.io/public/manasmods/manascore/maven/" }

    maven { url 'https://jitpack.io' }

    maven { url "https://maven.firstdarkdev.xyz/snapshots" }
    maven {url = "https://maven.blamejared.com/"} //JEI

    flatDir {
        dirs 'lib'
    }
}

dependencies {
    //Minecraft Forge
    minecraft "net.minecraftforge:forge:${minecraftVersion}-${forgeLoaderVersion}"

    // ManasCore and Dependencies
    implementation fg.deobf("com.github.manasmods:ManasCore:${minecraftVersion}-${manasCoreVersionCheck}:slim")
    implementation fg.deobf("com.lowdragmc.photon:photon-forge-${minecraftVersion}:${photonVersion}") {
        transitive = false
    }
    implementation fg.deobf("com.lowdragmc.ldlib:ldlib-forge-${minecraftVersion}:${ldlibVersion}") {
        transitive = false
    }
    implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:${mixinExtrasVersion}"))
    implementation("io.github.llamalad7:mixinextras-forge:${mixinExtrasVersion}")
    implementation fg.deobf("software.bernie.geckolib:geckolib-forge-1.19:${geckolib_version}.38")
    implementation fg.deobf("mezz.jei:jei-${minecraftVersion}-forge:${jeiVersionCheck}")

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    // Tensura
    implementation fg.deobf("blank:tensura:${minecraftVersion}-${tensuraVersionCheck}")
    implementation fg.deobf("com.github.glitchfiend:TerraBlender-forge:${minecraftVersion}-${terraBlenderVersionCheck}")

    // Ice and Fire
    implementation fg.deobf("curse.maven:citadel-331936:4556677")
    implementation fg.deobf("curse.maven:ice-and-fire-dragons-264231:5037952")
    implementation fg.deobf("curse.maven:atlasviewer-633577:4420382")
    implementation fg.deobf("curse.maven:mob-entity-path-visualizer-590716:4063117")
    implementation fg.deobf("curse.maven:terralith-513688:4823693")
}

mixin {
    add sourceSets.main, "${modId}.refmap.json"
    config "${modId}.mixins.json"
}

reobf {
    // jarJar {}
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "${modId}",
                "Specification-Vendor"    : "${modId}sareus",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "${modId}sareussareus",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "${modId}.mixins.json"
        ])
    }
}

def replaceResources = tasks.register("replaceResources", Copy) {
    it.outputs.upToDateWhen { false }
    //Copy it into the build dir
    it.from(sourceSets.main.resources) {
        def forgeLoaderVersion = forgeLoaderVersion.substring(0, forgeLoaderVersion.indexOf('.'))
        def nextMajorMinecraftVersion = getNextMajorMinecraftVersion(minecraftVersion)
        def minecraftVersionCheck = "[${minecraftVersion},${nextMajorMinecraftVersion})"
        def iafVersionCheck = "[1.19.2-2.1.13,)"
        def geckolibVersionCheck = "[${geckolib_version},)"
        def tensuraVersionCheck = "[1.0.0.0,)"
        def manasCoreVersionCheck = "[${manasCoreVersionCheck},)"

        include "META-INF/mods.toml"
        expand 'modVersion': "${modVersion}",
                'forgeLoaderVersion': "[${forgeLoaderVersion},)",
                'minecraftVersionCheck': "${minecraftVersionCheck}",
                'iafVersionCheck': "${iafVersionCheck}",
                'geckolibVersionCheck': "${geckolibVersionCheck}",
                'tensuraVersionCheck': "${tensuraVersionCheck}",
                'manasCoreVersionCheck': "${manasCoreVersionCheck}"

    }
    it.into "$buildDir/resources/main/"
}


processResources {
    duplicatesStrategy(DuplicatesStrategy.FAIL)
    exclude('META-INF/mods.toml')
    configure { finalizedBy(replaceResources) }
}

classes.configure {
    dependsOn(replaceResources)
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

static String getNextMajorMinecraftVersion(String currentVersion) {
    def version = currentVersion.substring(0, currentVersion.indexOf('.'))
    def majorVersion = currentVersion.substring(currentVersion.indexOf('.') + 1, currentVersion.indexOf('.', currentVersion.indexOf('.') + 1))
    def majorInt = Integer.valueOf(majorVersion)
    majorInt++
    majorVersion = majorInt.toString()
    return "${version}.${majorVersion}"
}

jar.finalizedBy('reobfJar')
